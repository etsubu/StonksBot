plugins {
    id "io.freefair.lombok" version "6.5.1"
    id "org.owasp.dependencycheck" version "7.2.1"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.github.ben-manes.versions" version "0.42.0"
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id "org.sonarqube" version "3.4.0.2513"
    id "java"
}

sonarqube {
    properties {
        property "sonar.projectKey", "etsubu_StonksBot_AYO7T27zZj0igeZfRk5j"
    }
}

repositories {
    mavenCentral()
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
}

test {
    useJUnitPlatform()
}

sourceSets {

    integrationTest {

        java.srcDir 'src/integration_test/java'
        resources.srcDir 'src/integration_test/resources'

        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}


configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    environment "oath1",  System.getenv('OATH1')
    environment "oath2",  System.getenv('OATH2')
}

integrationTest {
    useJUnitPlatform()
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    implementation 'org.springframework:spring-core:5.3.23'
    implementation 'org.springframework:spring-beans:5.3.23'
    implementation 'org.springframework:spring-context:5.3.23'
    implementation 'org.yaml:snakeyaml:1.33'
    implementation 'org.ta4j:ta4j-core:0.15'
    implementation 'com.google.code.gson:gson:2.9.1'
    implementation 'org.json:json:20220924'
    implementation ('net.dv8tion:JDA:5.0.0-alpha.21') {
        exclude module: 'opus-java'
    }
    implementation 'org.slf4j:slf4j-api:2.0.3'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.19.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.19.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.19.0'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.319'
    implementation 'com.amazonaws:aws-java-sdk-dynamodb:1.12.332'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.8.0'
}

group = 'com.etsubu.stonksbot'
version = '1.2-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

jar {
    manifest {
        attributes "Version" : project.version
        attributes "Main-Class": "com.etsubu.stonksbot.Main"
    }
}